# Check for updates on initial load...
if [ "$DISABLE_AUTO_UPDATE" != "true" ]
then
  /usr/bin/env zsh $ZSH/zsh/tools/check_for_upgrade.sh
fi

# Initializes Oh My Zsh

# add a function path
fpath=($ZSH/zsh/functions $ZSH/zsh/completions $fpath)

# Load all of the config files in ~/oh-my-zsh that end in .zsh
# TIP: Add files you don't want in git to .gitignore
for config_file ($ZSH/zsh/lib/*.zsh) source $config_file

# Add all defined plugins to fpath
plugin=${plugin:=()}
for plugin ($plugins) fpath=($ZSH/zsh/plugins/$plugin $fpath)

# Load and run compinit
autoload -U compinit
compinit -i

# Load all of the plugins that were defined in ~/.zshrc
for plugin ($plugins); do
  if [ -f $ZSH/zsh/custom/plugins/$plugin/$plugin.plugin.zsh ]; then
    source $ZSH/zsh/custom/plugins/$plugin/$plugin.plugin.zsh
  elif [ -f $ZSH/zsh/plugins/$plugin/$plugin.plugin.zsh ]; then
    source $ZSH/zsh/plugins/$plugin/$plugin.plugin.zsh
  fi
done

# Load all of your custom configurations from custom/
if [ -d "$ZSH/zsh/custom" ]; then
  for config_file ($ZSH/zsh/custom/*.zsh) source $config_file
fi

export ZSH_THEME="frisk"
# Load the theme
if [ "$ZSH_THEME" = "random" ]
then
  themes=($ZSH/zsh/themes/*zsh-theme)
  N=${#themes[@]}
  ((N=(RANDOM%N)+1))
  RANDOM_THEME=${themes[$N]}
  source "$RANDOM_THEME"
  echo "[oh-my-zsh] Random theme '$RANDOM_THEME' loaded..."
else
  if [ ! "$ZSH_THEME" = ""  ]
  then
    source "$ZSH/zsh/themes/$ZSH_THEME.zsh-theme"
  fi
fi

